on ^*:TEXT:*:#:{

  var %distanceRegex /((\d+|,|\.)+)\s?(kls|mls|ls|ly)/iS
  ;Use this regex instead to only match on callouts
  ;var %distanceRegex /#.*\s((\d+|,|\.)+)\s?(kls|mls|ls|ly)/iS
  if ( $nick != MechaSqueak[BOT] && $regex(lightDistance, $1-, %distanceRegex) ) {
    
    
    ;calc distance to time
    ;convert distance to float, works with a decimal (ie 5.65kls)
    ;does not work with orders of magntiude separators (ie 1,000,000ls)
    var %distance $round( $regml(lightDistance,1), 2 )


    ;convert distance into light seconds
    var %lastMatch $regml(lightDistance,0)
    if ( $regml(lightDistance,%lastMatch) == mls ) {
      var %ls $calc(%distance * 1000000)
    }
    elseif ( $regml(lightDistance,%lastMatch) == kls ) {
      var %ls $calc(%distance * 1000)
    }
    elseif ( $regml(lightDistance,%lastMatch) == ly ) {
      if ( %distance > 0.5 ) {
        return
      }
      var %ls $calc(%distance * 31557600)
    }
    elseif ( $regml(lightDistance,%lastMatch) == ls ) {
      var %ls %distance
    }
    else {
      ;unrecognized units, shouldn't have matched regex so this should be impossible
      return
    }
    
    ;calculate total seconds
    if (%ls < 100000) {    
      var %totalSeconds $int($calc( ( %ls ^ 0.3292 ) * 8.9034 ))
    }
    elseif (%ls < 1907087) {
      var %val1 $calc( -8 * 10 ^ -23 * %ls ^ 4 )
      var %val2 $calc( 4 * 10 ^ -16 * %ls ^ 3 - 8 * 10 ^ -10 * %ls ^ 2 )
      var %val3 $calc( 0.0014 * %ls + 264.79 )
      var %totalSeconds $int($calc( %val1 + %val2 + %val3 ))
    }
    else {
      var %totalSeconds $int($calc( ( %ls - 5265389.609 ) / 2001 + 3412 ))
    }
    
    ;convert to human readable
    var %hours $calc( %totalSeconds // 3600 )
    var %remainderSec $calc( %totalSeconds % 3600 )
    var %minutes $calc( %remainderSec // 60 )
    var %seconds $calc( %totalSeconds % 60 )
    
    ;format and add time estimate to distance
    if ( %hours > 0 ) {
      var %formattedTime ( $+ %hours $+ h $+ %minutes $+ m $+ %seconds $+ s)
    }
    elseif ( %minutes > 0 ) {
      var %formattedTime ( $+ %minutes $+ m $+ %seconds $+ s)
    }
    else {
      var %formattedTime ( $+ %seconds $+ s)
    }
    var %formattedTime $chr(29) $+ $chr(3) $+ $color(whois) %formattedTime $+ $chr(15)
    var %distanceAndTime $regml(lightDistance,1).full $+ %formattedTime 
        
    ;get nick mode char and color
    var %cmode $nick(#,$nick).cmode
    if ( $rgbcolors ) {
      var %color $rgb($nick(#,$nick).rgbcolor).hex
    }
    else {
      var %color $nickcolor
    }
    
    ;insert time into message
    noop $regsub($1-,%distanceRegex,%distanceAndTime,%subbedText)
    
    ;print the final result
    echo -vrlmbf $timestamp $chr(32) < $+ $chr(2) $+ $chr(4) $+ %color $+ %cmode $+ $nick $+ $chr(15) $+ > $chr(32) $+ %subbedText

    halt
  }

}